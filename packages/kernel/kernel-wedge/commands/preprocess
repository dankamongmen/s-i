#!/usr/bin/perl
use strict;
use warnings;
my $sysdir="/usr/share/kernel-wedge/modules/";

my %modules;
my %loaded;

sub loadlist {
	my $list=shift;
	
	if ($loaded{$list}) {
		die "include loop detected loading $list\n";
	}
	$loaded{$list}=1;
	
	my $fh;
	open ($fh, $list) || die "cannot read $list\n";
	while (<$fh>) {
		chomp;
		if (/^\s*#include\s+<(.*)>\s*$/) {
			my $basename=$1;
			loadlist($sysdir.$basename);
		}
		elsif (/^\s*#include\s+"(.*)"\s*$/) {
			my $include=$1;
			my ($dirname)=$list=~m!(.*/).*!;
			loadlist($dirname.$include);
		}
		elsif (/^\s*$/) {
			next;
		}
		elsif (/^\s*#/) {
			next;
		}
		elsif (/(.*) -$/) {
			delete $modules{$1};
		}
		else {
			s/^\s*//;
			s/\s*$//;
			$modules{$_}=1;
		}
	}
	close $fh;
}

my $file=shift || die "no input file given";
loadlist($file);
foreach my $m (sort keys %modules) {
	print "$m\n";
}
