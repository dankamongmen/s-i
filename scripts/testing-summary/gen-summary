#!/usr/bin/perl
# Summary of udebs in testing vs those in unstable.
use strict;
use warnings;

use File::Basename;
use File::Path;

my $mirror=shift || "/org/ftp.debian.org/ftp/";
# Die now if a local mirror can't be found:
if ($mirror =~ m{^/} and ! -d $mirror) {
	print STDERR "The local mirror $mirror can't be found, please specify another mirror.\n";
	print STDERR "Example for a remote mirror: $0 http://ftp.fr.debian.org/debian\n";
	print STDERR "Example for a local  mirror: $0 /srv/mirror/debian/\n";
	exit 1;
}

# only the arches we care about
my @arches=qw(amd64 armel armhf i386 ia64 kfreebsd-amd64 kfreebsd-i386 mips mipsel powerpc s390 s390x sparc);
my @dists=qw(testing unstable);

my %packageversions;
my %seenpackages;
my %pooldirs;
my %poolfiles;
my %lastbinfromsource;
my %inconsistent;
my %bin_nmu;
my %frozen;
my %has_debs;
my @annotations;
my %ages;

sub markup {
	my $text=shift;
	$text=~s/\#(\d\d\d\d+)/<a href="http:\/\/bugs.debian.org\/$1">#$1<\/a>/g;
	return $text;
}

sub between_versions {
	my $version=shift;
	my $low=shift;
	my $high=shift;
	if ((! length $low || system('dpkg','--compare-versions', $low, '<<', $version) == 0) &&
	    (! length $high || system('dpkg','--compare-versions', $high, '>=', $version) == 0)) {
		return 1;
	}
	else {
		return 0;
	}
}

sub older_version {
	my $version=shift;
	my $high=shift;
	if (length $high && system('dpkg','--compare-versions', $high, '>>', $version) == 0) {
		return 1;
	}
	else {
		return 0;
	}
}

sub archname {
	my %arch=map { $_ => 1 } @_;
	my %seen;
	my %unseen=map { $_ => 1 } @arches;
	foreach my $arch (@arches) {
		if (exists $arch{$arch}) {
			$seen{$arch}=1;
			delete $unseen{$arch}
		}
	}
	if (! %unseen) {
		return "all";
	}
	elsif (keys %unseen > keys %seen) {
		return join(",", keys %seen);
	}
	else {
		return join(",", map { "!$_" } keys %unseen);
	}
}

sub encode_str {
	my $str=shift;

	$str =~ s/([^A-Za-z0-9])/sprintf("%%%02X", ord($1))/seg;
}

# Sync files from release.debian.org:
# - dates
unlink 'Dates';
`wget -q http://release.debian.org/britney/data-b2/Dates`;
# - hints
unlink <hints/*>;
my $hints_url = 'http://release.debian.org/britney/hints/';
`wget -q -P hints $hints_url`;
open my $hints_file, '<', 'hints/index.html'
	or die "Unable to open hints file";
while (<$hints_file>) {
	if (m{<li><a href="(.*)">\1</a></li>}) {
		next if $1 eq 'README' or $1 eq 'satbritney';
		`wget -q -P hints $hints_url/$1`;
	}
}
close $hints_file
	or die "Unable to close hints file";


foreach my $dist (@dists) {
	foreach my $arch (@arches) {
		my $pkgdir  = "dists/$dist/main/debian-installer/binary-$arch";
		my $pkgfile = "$pkgdir/Packages.gz";
		# If not a local mirror, remove a (possible) file from
		# a previous run and download again:
		if ($mirror !~ m{^/}) {
			unlink $pkgfile;
			`wget -q -P $pkgdir $mirror/$pkgfile`;
		}
		else {
			$pkgfile = "$mirror/$pkgfile";
		}
		open(PKG, "zcat $pkgfile |") || die "cannot read $pkgfile: $!";
		local $/="\n\n";
		while (<PKG>) {
			my ($package)=m/Package: (.*)\n/;
			my ($version)=m/Version: (.*)\n/;
			if (! length $package || ! length $version) {
				print STDERR "Parse failure. Stanza: $_";
			}
			else {
				my ($source)=m/Source: (.*)\n/;
				if (! defined $source) {
					$source=$package;
				}
				else {
					# Source line can include a version if binNMU'ed
					$source=~s/ .*$//;
				}

				# Assume that source packages maintained by d-i team don't
				# have debs and others do; exceptions in deb-exceptions
				my ($maintainer)=m/Maintainer: (.*)\n/;
				if (defined $maintainer && $maintainer !~ /debian-boot/) {
					$has_debs{$source}=1;
				}

				# Check for binNMUs; correct version to source version
				if ($version =~ /\+b[1-9]$/) {
					if (! $bin_nmu{$source}) {
						$bin_nmu{$source}=1;
						push @annotations, {arch => $arch, package => $source, annotation => "has binary NMU in $dist" };
					}
					$version =~ s/\+b[1-9]$//
				}
				if ($dist eq 'testing' &&
				    exists $packageversions{$source}{$dist}{$arch} &&
				    $packageversions{$source}{$dist}{$arch} ne $version) {
					if (! $inconsistent{$source}) {
						$inconsistent{$source}=1;
						push @annotations, {arch => $arch, package => $source, annotation => "inconsistent binary versions in testing" };
					}
					push @annotations, {arch => $arch, detail => 1, package => $source, annotation => "binary package $package at version $version on arch $arch, while $lastbinfromsource{$source} is version $packageversions{$source}{$dist}{$arch}" };
				}
				else {
					$packageversions{$source}{$dist}{$arch} = $version;
					$lastbinfromsource{$source}=$package;
					$seenpackages{$source}=1;
					my ($filename)=m/Filename: (.*)\n/;
					push @{$poolfiles{$source}{$arch}}, $filename;
					my ($pooldir)=m/Filename: (.*)\/.*?\n/;
					$pooldirs{$source}=$pooldir;
				}
			}
		}
	}
	close PKG;
}

# Process exceptions for source packages with debs
if (open (EXC, "deb-exceptions")) {
	while (<EXC>) {
		next if /^#/;
		next unless length;

		my ($package)=m/^([^ ]*) [01]\n/;
		my ($has_deb)=m/^[^ ]* ([01])\n/;
		if (defined $package && defined $has_deb) {
			$has_debs{$package}=$has_deb;
		}
	}
	close EXC;
} else {
	print STDERR "failed to read package excepions file: $!\n";
}

# Collate transitions for multiple arches
my %transitions;
foreach my $package (keys %seenpackages) {
	foreach my $arch (@arches) {
		my $key="$package";
		my %seen;
		foreach my $dist (@dists) {
			if (exists $packageversions{$package}{$dist}{$arch}) {
				my $version=$packageversions{$package}{$dist}{$arch};
				$key.=" $version";
				$seen{$version}++;
			}
		}
		my $dup=0;
		foreach my $key (keys %seen) {
			$dup=1 if $seen{$key} > 1;
		}

		push @{$transitions{$package}{$key}}, $arch if ! $dup && %seen;
	}
}

if (open (ANN, "annotations")) {
	while (<ANN>) {
		chomp;
		next if /^#/;
		next unless length;

		my ($user, $package, $version, $annotation) = split(' ', $_, 4);
		next unless defined $annotation && length $annotation;
		push @annotations, {user => $user, package => $package,
			version => $version, annotation => markup($annotation)};
	}
	close ANN;
} else {
	print STDERR "failed to read annotations: $!\n";
}

# Read packages in freeze file
if (open (FREEZE, "hints/freeze")) {
	while (<FREEZE>) {
		next if /^#/;
		next unless length;

		my ($package)=m/block-udeb (.*)\n/;
		if (defined $package) {
			$frozen{$package}=1;
		}
	}
	close FREEZE;
} else {
	print STDERR "failed to read frozen packages file: $!\n";
}

# Read ages, mimicking britney's computation:
#   # britney's "day" begins at 3pm
#   self.date_now = int(((time.time() / (60*60)) - 15) / 24)
# Beware, there might be several versions in unstable, so track the
# version the age is specific to. The old version will be 'unknown'
# until it's built everywhere, only the latest version appears in
# Dates:
if (open (DATES, "Dates")) {
	my $now = int(((time / (60*60)) - 15 ) / 24);
	while (<DATES>) {
		if (/^(\S+) (\S+) (\S+)$/) {
			my ($package, $version, $days) = ($1, $2, $3);
			$ages{$package}{$version} = $now-$days;
		}
	}
} else {
	print STDERR "failed to read Dates file: $!\n";
}

my %normal_unblocks;
my %udeb_unblocks;
my %age_days;
for my $hint_file (<hints/*>) {
	open my $fh, '<', "$hint_file"
		or die "Unable to open $hint_file";
	my $hinter = basename($hint_file);
	while (my $line = <$fh>) {
		# Skip a bunch of things we don't care about:
		next if $line =~ /^\s*#/;
		next if $line =~ /^\s*$/;
		# End here:
		last if $line =~ /^finished/;
		# Interesting things start here:
		if ($line =~ /^unblock\s+(.*)/) {
			for my $p (split /\s+/, $1) {
				push @{$normal_unblocks{$p}}, $hinter;
			}
			next;
		}
		if ($line =~ /^unblock-udeb\s+(.*)/) {
			for my $p (split /\s+/, $1) {
				push @{$udeb_unblocks{$p}}, $hinter;
			}
			next;
		}
		if ($line =~ /^age-days (\d+) (.*)/) {
			for my $p (split /\s+/, $2) {
				push @{$age_days{$p}}, "$hinter: $1";
			}
			next;
		}
		if ($line =~ /^urgent (.*)/) {
			for my $p (split /\s+/, $1) {
				push @{$age_days{$p}}, "$hinter: U";
			}
			next;
		}
		#print "Unused: [$h] $line";
	}
	close $fh
		or die "Unable to close $hint_file";
}


print "<html><head><title>udeb testing summary</title></head><body>\n";

print "<h1>udeb testing summary</h1>\n";

print "<table border=1><tr align=left><th>source package</th><th>debs</th><th>arches</th>";
foreach my $dist (@dists) {
	print "<th>$dist</th>";
}
print "<th>age (days)</th><th>frozen</th><th>unblock</th><th>udeb-unblock</th><th>age-days</th><th>utils</th><th>commits in git</th></tr>\n";
my $numcols=9 + @dists;

foreach my $package (sort keys %seenpackages) {
	foreach my $transition (keys %{$transitions{$package}}) {
		print "<tr><td><a href=\"http://packages.debian.org/unstable/source/$package\">$package</a></td>";
		if (exists $has_debs{$package} && $has_debs{$package}==1) {
			print "<td align=\"center\">x</td>";
		} else {
			print "<td>\&nbsp;</td>";
		}
		print "<td>".archname(@{$transitions{$package}{$transition}})."</td>";
		my $some_arch=@{$transitions{$package}{$transition}}[0];
		my @vers;
		my $unstable_version;
		foreach my $dist (@dists) {
			my $version=$packageversions{$package}{$dist}{$some_arch};
			if (! defined $version) {
				$version="-";
				push @vers, '';
				print "<td>-</td>";
			}
			else {
				push @vers, $version;
				if ($dist eq 'testing') {
					print "<td><a href=\"http://release.debian.org/migration/testing.pl?package=" . encode_str($package) . "\">$version</a></td>";
				}
				else {
					print "<td><a href=\"http://packages.debian.org/changelog:$package\">$version</a></td>";
					$unstable_version = $version;
				}
			}
		}
		my $minage = defined($ages{$package}{$unstable_version}) ? $ages{$package}{$unstable_version} : 'unknown';
		print "<td>".$minage."</td>";
		if (exists $frozen{$package}) {
			print "<td align=\"center\">yes</td>";
		} else {
			print "<td align=\"center\"><font color=\"red\">NO</font></td>";
		}

		print "<td>" . (join('<br>', @{$unstable_version ? $normal_unblocks{"$package/$unstable_version"}||[] : []})) . "</td>";
		print "<td>" . (join('<br>', @{$unstable_version ? $udeb_unblocks{"$package/$unstable_version"}  ||[] : []})) . "</td>";
		print "<td>" . (join('<br>', @{$unstable_version ? $age_days{"$package/$unstable_version"}       ||[] : []})) . "</td>";

		print "<td><a href=\"http://release.debian.org/migration/testing.pl?package=$package\">t</a>
               <a href=\"http://buildd.debian.org/status/package.php?p=$package\">b</a>
               <a href=\"http://packages.qa.debian.org/$package\">s</a></td>";

		my $commits = '?';
		my $gitdir = "../../packages/$package";
		if (-d $gitdir) {
			my $desc = `cd $gitdir && git describe --tags`;
			$commits = $desc =~ /-(\d+)-g/ ? $1 : 0;
		}
		print "<td>" . $commits . "</td>";
		print "</tr>\n";

		foreach my $ann (@annotations) {
			if ($package eq $ann->{package} &&
			    (! exists $ann->{version} || between_versions($ann->{version}, $vers[0], $vers[1])) &&
			    (! exists $ann->{arch} || grep $ann->{arch}, @{$transitions{$package}{$transition}}[0])) {
				print "<tr><td></td><td colspan=".($numcols-1)."><b>$ann->{annotation}</b>";
				if (exists $ann->{user}) {
				print " <i>-- $ann->{user}</i>";
				}
				if (exists $ann->{version} && older_version($ann->{version}, $vers[1])) {
					print " (re an older version)";
				}
				print "</td></tr>\n";
			}
		}
	}
}

print "</table><hr>\n";

my $date=`LANG=C TZ=GMT date`;
chomp $date;

print <<"EOS"
To update annotations on this web page, edit:
<code>svn://svn.debian.org/svn/d-i/trunk/scripts/testing-summary</code>
(no conversion to git for this part of d-i).
<p>
See also:
<ul>
<li>
<a href="http://ftp-master.debian.org/testing/hints/" target="_blank">dak hint files (RMs)</a>
</li><li>
<a href="http://ftp-master.debian.org/d-i" target="_blank">consistency check of sources of udebs</a>
</li><li>
<a href="http://ftp-master.debian.org/~vorlon/d-i-builddeps.html" target="_blank">consistency check of build dependencies</a>
</li>
</ul>
<hr>
$date
</html>
EOS
